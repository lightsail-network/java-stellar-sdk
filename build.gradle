buildscript {
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath 'com.github.ben-manes:gradle-versions-plugin:0.20.0'
  }
}

plugins {
  id "com.github.johnrengelman.shadow" version "2.0.4"
}

apply plugin: "com.github.johnrengelman.shadow"
apply plugin: 'java'
apply plugin: 'ivy-publish'
apply plugin: 'com.github.ben-manes.versions' // gradle dependencyUpdates -Drevision=release
apply plugin: 'project-report' // gradle htmlDependencyReport

sourceCompatibility = 1.6
version = '0.3.2'
group = 'stellar'

jar {
    manifest {
        attributes 'Implementation-Title': 'stellar-sdk',
                   'Implementation-Version': version
    }
    archiveName 'stellar-sdk.jar'
    duplicatesStrategy DuplicatesStrategy.EXCLUDE
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
}

publishing {
    publications {
        ivy(IvyPublication) {
            from components.java
        }
    }
    repositories {
        ivy {
            url "$projectDir/repos"
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile 'com.squareup.okhttp3:okhttp:3.11.0'
    compile 'com.moandjiezana.toml:toml4j:0.7.2'
    compile 'com.google.guava:guava:26.0-android'
    compile 'com.google.code.gson:gson:2.8.5'
    compile 'commons-io:commons-io:2.6'
    compile 'org.glassfish.jersey.core:jersey-client:2.27'
    compile 'org.glassfish.jersey.media:jersey-media-sse:2.27'
    compile fileTree(dir: 'libs', include: '*.jar')
    testCompile 'org.mockito:mockito-core:2.21.0'
    testCompile 'com.squareup.okhttp3:mockwebserver:3.11.0'
    testCompile 'junit:junit:4.12'
}

jar.enabled = false

assemble.dependsOn(shadowJar);

shadowJar {
   archiveName 'stellar-sdk.jar'
   relocate 'com','shadow.com'
   relocate 'net','shadow.net'
   relocate 'org.glassfish','shadow.org.glassfish'
   relocate 'org.mockito','shadow.org.mockito'
   relocate 'org.aopalliance','shadow.org.aopalliance'
   relocate 'org.apache','shadow.org.apache'
   relocate 'org.jvnet','shadow.org.jvnet'
   relocate 'org.objenesis','shadow.org.objenesis'
   relocate 'org.codehaus','shadow.org.codehaus'
   relocate 'org.checkerframework','shadow.org.checkerframework'
   relocate 'javassist','shadow.javassist'
   relocate 'javax.validation', 'shadow.javax.validation'
   relocate 'javax.annotation', 'shadow.javax.annotation'
   relocate 'javax.inject', 'shadow.javax.inject'
   relocate 'javax.ws', 'shadow.javax.inject'
   relocate 'commons-io', 'shadow.commons-io'
   relocate 'jersey', 'shadow.jersey'
   relocate 'okhttp3','shadow.okhttp3'
   relocate 'okio','shadow.okio'
}
