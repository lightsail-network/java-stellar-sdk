buildscript {
    ext.okhttpclientVersion= '3.11.0'
}

plugins {
    id "io.freefair.lombok" version "6.4.1"
    id "com.github.johnrengelman.shadow" version "7.1.2"
    id "java"
    id "com.github.ben-manes.versions" version "0.42.0"
    id "project-report"
    id "maven-publish"
    id "java-library"
}

sourceCompatibility = 1.6
version = '0.34.0'
group = 'stellar'
jar.enabled = false

publishing {
    publications {
        sdkLibrary(MavenPublication) { publication ->
            project.shadow.component(publication)
        }
    }
}

shadowJar {
    manifest {
        attributes (
            "Implementation-Title" : "stellar-sdk",
            "Implementation-Version" : project.getVersion()
        )
    }
    duplicatesStrategy DuplicatesStrategy.EXCLUDE
    archiveClassifier.set('')
    archiveFileName.set('stellar-sdk.jar')
    relocate 'com.','shadow.com.'
    relocate 'net.','shadow.net.'
    relocate 'javax.annotation', 'shadow.javax.annotation'
    relocate 'org.apache','shadow.org.apache'
    relocate 'org.jvnet','shadow.org.jvnet'
    relocate 'org.codehaus','shadow.org.codehaus'
    relocate 'org.threeten','shadow.org.threeten'
    relocate 'org.checkerframework','shadow.org.checkerframework'
    relocate 'okhttp3','shadow.okhttp3'
    relocate 'okio','shadow.okio'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation "com.squareup.okhttp3:okhttp:${okhttpclientVersion}"
    implementation "com.squareup.okhttp3:okhttp-sse:${okhttpclientVersion}"
    implementation 'com.moandjiezana.toml:toml4j:0.7.2'
    // use the android version because we don't want java 8 stuff
    implementation 'com.google.guava:guava:26.0-android'
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation 'commons-io:commons-io:2.6'
    implementation 'net.i2p.crypto:eddsa:0.3.0'
    implementation 'org.threeten:threetenbp:1.4.4'

    testImplementation 'org.mockito:mockito-core:2.21.0'
    testImplementation "com.squareup.okhttp3:mockwebserver:${okhttpclientVersion}"
    testImplementation 'junit:junit:4.12'
    testImplementation 'javax.xml.bind:jaxb-api:2.3.0'
    testImplementation group: 'org.junit.vintage', name: 'junit-vintage-engine', version: '4.12.1'
}
tasks.named('test') { task ->
    useJUnitPlatform()
}
